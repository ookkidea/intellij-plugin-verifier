buildscript {

  ext.kotlin_version = '1.2.41'

  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def extractorVersion = '1.0'
def intellijStructureVersion = 'dev'

configure(allprojects) {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'maven-publish'

  version = extractorVersion
  group = 'org.jetbrains.intellij.plugins'

  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
    mavenLocal()
    maven { url "http://dl.bintray.com/jetbrains/intellij-plugin-service" }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
  }
}

dependencies {
  compile "org.jetbrains.intellij.plugins:structure-intellij-classes:$intellijStructureVersion"
  compile "org.jetbrains.intellij.plugins:structure-ide-classes:$intellijStructureVersion"
  compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
  compile group: 'commons-io', name: 'commons-io', version: '2.5'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile project(':test-classes')
}

task sourcesJar(type: org.gradle.api.tasks.bundling.Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts.archives sourcesJar

publishing {

  publications {

    def configurePublication = { org.gradle.api.publish.maven.MavenPublication pub ->

      pub.groupId group
      pub.artifactId name
      pub.version version

      pub.from components.java
      artifacts
      pub.artifact sourcesJar
    }

    ProjectPublication(org.gradle.api.publish.maven.MavenPublication) { configurePublication(it) }
  }
}

publishToMavenLocal.dependsOn test