buildscript {

  ext.kotlin_version = '1.2.50'

  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def intellijPluginStructureVersion = 'dev'
if (project.hasProperty("structureVersion")) {
  intellijPluginStructureVersion = project.structureVersion
}

configure(allprojects) {
  group = 'org.jetbrains.intellij.plugins'
  version = intellijPluginStructureVersion

  apply plugin: 'java'
  apply plugin: 'kotlin'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "http://dl.bintray.com/jonnyzzz/maven" }
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
    maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
  }

  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  }

  //build .jar with sources
  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

publishing {
  publications {

    def configurePublication = { MavenPublication pub, String projectName ->

      def proj = project(projectName)
      pub.groupId proj.group
      pub.artifactId proj.name
      pub.version proj.version

      pub.from proj.components.java
      proj.artifacts
      pub.artifact proj.sourcesJar
    }

    BasePublication(MavenPublication) { configurePublication(it, ':structure-base') }
    ClassesPublication(MavenPublication) { configurePublication(it, ':structure-classes') }
    IntellijPublication(MavenPublication) { configurePublication(it, ':structure-intellij') }
    IntellijClassesPublication(MavenPublication) { configurePublication(it, ':structure-intellij-classes') }
    IdePublication(MavenPublication) { configurePublication(it, ':structure-ide') }
    IdeClassesPublication(MavenPublication) { configurePublication(it, ':structure-ide-classes') }
    TeamCityPublication(MavenPublication) { configurePublication(it, ':structure-teamcity') }
    DotNetPublication(MavenPublication) { configurePublication(it, ':structure-dotnet') }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')


  publish = true
  publications = ['BasePublication', 'ClassesPublication', 'IntellijPublication', 'IntellijClassesPublication', 'IdePublication', 'IdeClassesPublication', 'TeamCityPublication']

  pkg {
    repo = 'intellij-plugin-service'
    name = 'intellij-plugin-structure'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/JetBrains/intellij-plugin-verifier'
    userOrg = 'jetbrains'

    version {
      name = project.version
    }
  }
}

bintrayUpload.doFirst {

  if (!bintray.user || !bintray.key) {
    throw new StopExecutionException("You must specify (bintrayUser, bintrayApiKey) or (BINTRAY_USER, BINTRAY_API_KEY) properties")
  }

  println('Uploading IntelliJ structure ' + project.version + ' to bintray')
}

build.dependsOn project("tests").test
publishToMavenLocal.dependsOn test
bintrayUpload.dependsOn test